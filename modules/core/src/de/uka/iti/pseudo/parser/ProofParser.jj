
//
// This file contains the grammar for structural proofs.
//

ASTProofScript ProofScript():
{
  Token t = null;
  Token id = null;
  Token first;
  ProofScript.Kind kind = null;
  ASTProofScriptNode tree;
}
{
  first = "proof"

  (
    ( t = "rule" { kind = ProofScript.Kind.RULE; }
    | t = "program" { kind = ProofScript.Kind.PROGRAM; }
    | t = "problem" { kind = ProofScript.Kind.PROBLEM; }
    )

    id = <IDENTIFIER>
  )?

  "(" tree=ProofTree() ")"

    {
          return new ASTProofScript(first, kind, id, tree);
    }
}

ASTProofScriptNode ProofTree():
{
  ASTProofScriptNode result;
  Token t, t2;
  ASTProofScriptNode child;
}
{
  t = <IDENTIFIER>    {  result = new ASTProofScriptNode(t);  }

  ( t = <IDENTIFIER>
    ( ":" ( t2=<STRING> { result.put(t, t2, Util.stripQuotes(t2.image)); }
          | t2=<IDENTIFIER> { result.put(t, t2, t2.image); }
          )
    | /* epsilon */ { result.putUnnamed(t, t.image); }
    )
  | t=<STRING> { result.putUnnamed(t, Util.stripQuotes(t.image)); }
  )* 

  ( ";" child=ProofTree() { result.addChild(child); }
  | ( "(" child=ProofTree() { result.addChild(child); } ")" )*
  )

  { return result; }
}