/*
 * Selection Sort in Pseudo code
 */

uses "selectionSort.decl.p"

algo selectionSort

input
  a : array(int)
  
var
  i,j,t,n : int
  
output
  b : array(int)
  
ensures
  isSorted(b)
  
ensures
  isPerm(a,b)
  
do
  b := a ;;
  i := 1 ;;
  n := length(a) ;;
  
  if n = 0
  then 
    return
  end ;;
  
  while i < n
    inv i >= 1 & i <= n & 
        (\forall k; (\forall l; 
          1<=k & k<=l & l<=i -> read(b,k) <= read(b,l))) &
        (\forall k; (\forall l;
          1<=k & k<i & i<=l & l<=n -> read(b,k) <= read(b,l))) &
        isPerm(a, b) &
        length(b) = n
    var n - i
  do
    t := i ;;
    j := i+1 ;;
    while j <= n
      inv (\forall k; i<=k & k<j -> read(b,t) <= read(b, k))
        & (\forall k; (\forall l; 
             1<=k & k<=l & l<=i -> read(b, k) <= read(b, l)))
        & (\forall k; (\forall l;
             1<=k & k<i & i <= l & l<=n -> read(b, k) <= read(b,l)))
        & 1<=i & i<n
        & i+1<=j & j<=n+1
        & i<=t & t<=n
        & isPerm(a, b)
        & length(b) = n
      var (n - j) + 1
    do
      if read(b, j) < read(b, t) 
      then
        t := j
      end ;;
      j := j+1
    end ;;
    
    b := swap(b, i, t) ;;
    i := i+1
  end
end